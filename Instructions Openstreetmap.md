Інколи потрібно створити власну карту з доволі специфічними параметрами. Найкарщим джерелом цих даних слугує Openstreetmap.

## Openstreetmap
Одним з найпопулярнішим джерелом отимання даних є Openstreetmap, який містить інформацію щодо координат і багато іншої географічної інформації щодо всіх країн, громад, сіл і навіть озер чи ставків.
Існує як і API для витягу даних так і платформа, яка одразу допомагає візуалізувати запит для витягу дані.
<br>
Якщо ви бажаєте витягнути лише декількох файлів з інформацією або ви ще не надто впевнено себе відчуваєте з API, Overpass turbo є найкращим рішенням оскільки візуалізація одразу показує які дані є зайві, а яких не хватає.

## Overpass turbo

Overpass turbo [(overpass-turbo.eu)](overpass-turbo.eu) — це веб-інструмент інтелектуального аналізу даних для OpenStreetMap.
Приклад коду який витягуватиме дані всіх громад Київська області
```
[out:json][timeout:25];  
area['admin_level'='4']['name:uk'='Київська область'];
(rel['admin_level'='7'](area););
out geom;
```


Перший рядок `[out:json]` вказує дані якого формату ми бажаємо витягнути
Також можливі варінати витяг даних формату xml `[out:xml]` чи `csv`, при цьому для файлів формату csv обов'язково вказувати теги, які ми бажаємо витягнути `[out:csv(::type,::id,name,"name:en")]` та [інші](https://wiki.openstreetmap.org/wiki/Overpass_API/Overpass_QL#Output_format_(out:))
`[timeout:25]` обмежує час виконання запиту до 25 секунд. Якщо щось піде не так, то ліміт гарантує, що сервер не витрачатиме час.


Другий рядок `area['admin_level'='4']['name:uk'='Київська область']` фільтрує дані вказуючи, що ми хочемо отримати  дані, які знаходяться в межах  де admin_level=4 (тобто область) і називається "Київська область". Якщо ми бажаємо отримати дані по всій країні, то варто змінити рядок на `area['admin_level'='2']['name:uk'='Україна']`


Третій рядок `(rel['admin_level'='7'](area);)` вказує що ми хочемо отримати, в даному випадку це `rel` (відносини), інші можливі ваіранти це `node` і `way`. Географічна інформація про широту/довжину завжди зберігається у вузлах. Області з понад 2000 вузлів не можуть бути представлені шляхом, ця функція вимагатиме складнішої багатополігональної структури відсини.
Нарешті, `out geom` виводить відношення (включаючи їх дочірні шляхи та вузли). Існують багато інших варіантів оператору виходу, вони гарно описані [тут](https://dev.overpass-api.de/overpass-doc/en/targets/formats.html)

### Деякі приклади
Приклад коду який витягуватиме дані всіх областей України

```
[out:json];
area['admin_level'='2']['name:uk'='Україна'];
(rel['admin_level'='4'](area););
out geom;
```

Приклад коду який витягуватиме дані всіх районів України* (після реформи децентралізації)

```
[out:json];
area['admin_level'='2']['name:uk'='Україна'];
(rel['admin_level'='6'](area););
out geom;
```

Приклад коду який витягуватиме дані районів до реформи децентралізації.
Вказуючи дату ми отримуємо карту, яка існувала на момент зазначений в коді, майте на увазі це значно сповільнює виконання коду і чим далі в минули тим більш дані "брудні". Тут ми також вказуємо фільтру, що варто шукати 'uk', див пояснення нижче (проблема сусіда).

```
[date:"2014-01-01T19:00:00Z"]
[out:json];
area['admin_level'='4']['name:uk'='Львівська область'];
(rel['admin_level'='6']['wikipedia' ~'uk'](area););
out geom;

```

Приклад коду який витягуватиме дані всіх громад України*

```
[out:json];
area['admin_level'='2']['name:uk'='Україна'];
(rel['admin_level'='7'](area););
out geom;
```

Приклад коду який витягуватиме дані всіх районів Києва

```
[out:json];
area['admin_level'='4']['name:uk'='Київ'];
(rel['admin_level'='10'](area););
out geom;
```
**(за виключенням тимчасово окупованих тереторій)*

## Виправлення даних

Для тестування, щоб зменшити обсяг даних, використаємо функцію `[bbox]`, яка повертатиме дані лише в області, яка попадає в межі відображення екрану `[bbox:{{bbox}}]` або в зазначених координатах `[bbox:49.560852, 22.617202, 50.659908, 24.490371]`

### Проблема сусіда

Очікувано, що запит нижче поверне координати міста Києва, але він поверне також Київську область.
```
[out:json];
area['admin_level'='4']['name:uk'='Київ'];
(rel['admin_level'='4'](area););
out geom;
```
Проблема в тому, що запит повертатиме шляхи, які є частинами *relative* міста Київа, оскільки Київ межує з Київсьою областю (тобтот має спільні шляхи) і Київська область теж має `'admin_level'='4'`, тому ми отримуємо дві сутності Київ та Київська область.  Ця помилка може вирішитися специфікацією, що ми шукаємо саме місто.

```
[out:json];
area['admin_level'='4']['name:uk'='Київ'];
(rel['admin_level'='4']['place'='city'](area););
out geom;
```
Якщо ці сутності мають одинаковий тип, наприклад в випадку прикордонних областей, які межуть з іншими країнами, ми можемо відфільтрувати їх за іншим тегами, напрклад для області вказати `["iso3166-2"~"^UA-"]` де для позначення України використовується  *UA* , а знак `~` означає шукати відповідний регулярний вираз.


### Об'єднання даних

Місто Київ не є районом, проте якщо ми витягуватимео дані по всіх районах Київської області, то на мапі України в нас буде пусте місце яке належитеме місту Києву. Отже припустимо ми бажаємо отримати всі райони Київської області і міста Київ. Оскільки admin_level для міста Києва і області одинаковий, то варто вказати, що ми шукаємо саме місто  `['place'='city']`, інакше отримаємо дублювання даних (проблема сусіда).

Після цього, якщо запити не суперечать одне одному ми можемо їх просто обєднати. 
Їх можна записати як і окремо, так і віднести параметри в змінні:

```
[out:json];
area['admin_level'='4']['name:uk'='Київська область'];
(rel['admin_level'='6'](area););
out geom;
area['admin_level'='4']['name:uk'='Київ'];
(rel['admin_level'='4']['place'='city'](area););
out geom;
```

```
[out:json];
area['admin_level'='4']['name:uk'='Київська область']->.area1;
area['admin_level'='4']['name:uk'='Київ']->.area2;
(
rel['admin_level'='6'](area.area1) ->.region;
rel['admin_level'='4']['place'='city'](area.area2) ->.city;
);
out geom;
```

### Морський кордон

Запит 
```
[out:json];
area['name:uk'='Одеська область'];
(rel['admin_level'='4'](area););
out geom;
```
Поверне межі Одеської області, а також зону протяжністю 12 морських миль (приблизно 22 км) від базової лінії. Це не зовсім так, як ми уявляємо мапу, тож зазвичай ми не хочемо бачити таке відображення мапи. 
Найпростіший спосіб виправлення це витягнути дані, з більшою деталізацією і потім обєднати ці дані. 

```
[out:json];
area['name:uk'='Одеська область'];
(rel['admin_level'='6'](area););
out geom;
```

Інший спосіб який поверне лише межі:

```
[out:json];
area['name:uk'='Одеська область']->.region;
rel['name:uk'='Одеська область']['admin_level'='4'];
(
way(r)["maritime" != "yes"];
way(area.region)["natural"="coastline"];
);
out geom;
```

## Витяг даних

Якщо ми використовуємо Overpass turbo, то, звичайно, все залежить від задачі, але найбільш комфортним пропонованим форматом є `GeoJSON`, тому для витягу даних потрібно  натиснути "Експорт" -> "GeoJSON". 

Якщо ви використовуєте API, то запит за допомогою Python виглядатиме так:

```
import requests

overpass_url = "https://overpass-api.de/api/interpreter?data="
overpass_query = '''
[out:json];
area['name:uk'='Одеська область'];
(rel['admin_level'='6'](area););
out geom;
'''
response = requests.get(overpass_url,  params={'data': overpass_query})
data = response.json()
print(data)
```

## JSON vs GeoJSON vs TopoJSON

Pезультатом API запиту буде JSON файл. Для роботи з координатами **JSON**  формат це не найкращий формат, тому краще перетворювати дані в формат `GeoJSON` після чого дані можна обробляти за допомогою різноманітних бібліотек. 

GeoJSON — відкритий формат призначений для зберігання географічних структур даних, заснований на JSON.
Подальшим розвитком GeoJSON є TopoJSON, розширення GeoJSON, яке кодує геопросторову топологію, і, як правило, забезпечує менший розмір файлів.


Загалом **TopoJSON** кращий формат для кінцевого результату, він  усуває надмірність, пропонуючи набагато більш компактні представлення геометрії, ніж у GeoJSON.  Типовий файли TopoJSON на 80% менші за їхні еквіваленти GeoJSON. Крім того, TopoJSON полегшує роботу програм, які використовують топологію, наприклад спрощення форми із збереженням топології, автоматичне фарбування карт і картограм. Через ці переваги, якщо ви хочете завантажити власне карту в PowerBI вам потрібно буде використовувати дані формату TopoJSON


Проте, якщо ви плануєте працювати з даними, їх обробляти, змінювати, кращим форматом є **GeoJSON** оскільки для нього існує більше більше бібліотек. Загалом через структуру даних в TopoJSON, де дані координат стають цілим числом за допомогою констант трансляції/масштабування, то стан кожної пари координат набагато важчий для розуміння, аніж в GeoJSON. 

## Mapshaper

Після того,  як ви витягнули якісь дані, не завжди є потреба запускати Python чи інші спеціальні програми, часто потрібні лише деякі елементарні зміни чи перегляд. Для цього є безліч сайтів, але моїм улюбленим є [https://mapshaper.org/](https://mapshaper.org/). 

Mapshaper - це програмне забезпечення для редагування Shapefile, GeoJSON, TopoJSON, CSV та  інших форматів даних. Опубліковано як інструмент CLI так і веб сайт. 
Сайт дозволяє переглядати дані, зберігати/конвертувати їх в різних форматах, обєднувати дані і багато іншого. 

[Документаці](https://github.com/mbloch/mapshaper/wiki) по використанню Mapshaper. [Інструкція](https://handsondataviz.org/mapshaper.html) щодо основних функції з картинками.  




